# Maintainer: Aditya Shakya <adi1090x@gmail.com>

pkgname=archcraft-2bwm
pkgver=2.0
pkgrel=5
pkgdesc="2BWM for Archcraft"
arch=('any')
url="https://github.com/archcraft-os/archcraft-2bwm"
license=('GPL3')
depends=('xcb-util-wm' 'xcb-util-keysyms' 'xcb-util-xrm' 'hsetroot' 'xsettingsd'
		'pulsemixer' 'light' 'polybar' 'rofi' 'dunst'
)
makedepends=('git' 'xcb-util')
optdepends=('alacritty: default terminal emulator'
			'thunar: default file manager'
			'geany: default text editor'
			'firefox: default web browser'
			'viewnior: default image viewer'
			'betterlockscreen: default lockscreen'
			'ksuperkey: allows you to open the application launcher using the Super key'
			'networkmanager-dmenu-git: control NetworkManager via rofi'
			'mpd: server-side application for playing music, used in statusbars and scripts'
			'mpc: minimalist command line interface to MPD'
			'ffmpeg: complete solution to record, convert and stream audio and video, used in screenrecord scripts'
			'maim: utility to take a screenshot, used in screenshot scripts'
			'xclip: command line interface to the X11 clipboard'
			'xcolor: lightweight color picker for X11'
			'xfce4-power-manager: power manager'
			'xorg-xsetroot: fix cursor theming, set root background'
			'yad: display graphical dialogs from shell scripts'
			'wmname: utility to set the name of your window manager'
)
conflicts=('2bwm' '2bwm-git' 'mcwm' 'mcwm-git') # due to /usr/bin/hidden program
options=(zipman !strip !emptydirs)
install="${pkgname}.install"

prepare() {
	cp -af ../source/. "$srcdir"
	cp -af ../source "$srcdir"
	cp -af ../shared "$srcdir"
}

build() {
	cd "$srcdir"
	make PREFIX=/usr
}

package() {
	cd "$srcdir"
	make DESTDIR="$pkgdir" PREFIX=/usr MANPREFIX=/usr/share/man install
	install -Dm644 "$srcdir/2bwm.desktop" "$pkgdir/usr/share/xsessions/2bwm.desktop"
	
	## Install shared files
	local _wmdir="$pkgdir"/usr/share/archcraft
	mkdir -p "$_wmdir"
	
	cp -r "$srcdir"/shared  "$_wmdir"/2bwm
	chmod +x "$_wmdir"/2bwm/scripts/*
	chmod +x "$_wmdir"/2bwm/theme/polybar/scripts/bluetooth.sh
	
	## Copy Source files in /opt
	local _wmsrc="$pkgdir"/opt/2bwm-source
	mkdir -p "$_wmsrc"
	
	cp -r "$srcdir"/source/*  "$_wmsrc"
}
